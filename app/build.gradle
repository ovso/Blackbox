apply plugin: 'com.android.application'
apply plugin: 'hugo'
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  bundle {
    density.enableSplit = true
    abi.enableSplit = true
    language.enableSplit = true
  }
  signingConfigs {
    release {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
  }
  compileSdkVersion rootProject.compile_sdk_version
  defaultConfig {
    applicationId rootProject.application_id
    minSdkVersion rootProject.min_sdk_version
    targetSdkVersion rootProject.target_sdk_version
    versionCode rootProject.version_code

    int versionMajor = 0
    int versionMinor = 0
    int versionPatch = rootProject.version_code

    if (1000 > versionPatch && versionPatch > 99) {
      versionMajor = versionPatch / 100
      versionMinor = (versionPatch % 100) / 10
      versionPatch = (versionPatch % 100) % 10
    } else if (100 > versionCode && versionPatch > 9) {
      versionMajor = 0
      versionMinor = versionPatch / 10
      versionPatch = versionPatch % 10
    } else if (versionPatch < 10) {
      versionMajor = 0
      versionMinor = 0
      versionPatch = versionPatch / 1
    }

    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    //multiDexEnabled true
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled false
      shrinkResources false
      useProguard false
      proguardFile 'proguard-butterknipe.pro'
      proguardFile 'proguard-glide.pro'
      proguardFile 'proguard-retrofit2.pro'
      proguardFile 'proguard-dagger.pro'
      proguardFile 'proguard-okhttp3.pro'
      proguardFile 'proguard-gson.pro'
      proguardFile 'proguard-firebase.pro'
      proguardFile 'proguard-android-support.pro'
      proguardFile 'proguard-others.pro'

      android.applicationVariants.all { variant ->
        variant.outputs.all {
          def formattedDate = new Date().format('yyyyMMddHHmmss')
          def appName = variant.getApplicationId().split(/[.]/)[3]
          outputFileName =
              "${appName}-${variant.name}-${variant.versionCode}-${variant.versionName}-${formattedDate}.apk"
        }
      }
    }
    debug {
    }
  }

  repositories {
    maven {
      url 'https://maven.google.com'
      // Alternative URL is 'https://dl.google.com/dl/android/maven2/'
    }
    maven { url 'https://maven.fabric.io/public' }
  }

  compileOptions {
    targetCompatibility JavaVersion.VERSION_1_8
    sourceCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  testImplementation 'junit:junit:4.12'

  implementation files('libs/YouTubeAndroidPlayerApi.jar')
  implementation "com.android.support:appcompat-v7:$support_version"
  implementation "com.android.support:support-v4:$support_version"
  implementation "com.android.support:support-media-compat:$support_version"
  implementation "com.android.support:design:$support_version"
  implementation "com.android.support:customtabs:$support_version"
  implementation "com.android.support.constraint:constraint-layout:$constraint_version"
  implementation "com.android.support:cardview-v7:$support_version"

  implementation "com.google.firebase:firebase-ads:$firebase_ads_version"
  implementation "com.google.firebase:firebase-core:$firebase_core_version"
  implementation "com.crashlytics.sdk.android:crashlytics:$firebase_crashlytics_version"

  implementation "com.google.dagger:dagger:$dagger_version"
  implementation "com.google.dagger:dagger-android-support:$dagger_version"
  annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
  annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

  implementation "com.google.code.findbugs:jsr305:$jsr305_version"
  implementation "com.github.bumptech.glide:glide:$glide_version"



  implementation "com.jakewharton:butterknife:$butterknife_version"
  annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"

  implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
  implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
  implementation "com.jakewharton.rxbinding2:rxbinding:$rxbinding2_version"

  implementation "com.jakewharton.timber:timber:$timber_version"

  compileOnly "org.projectlombok:lombok:$lombok_version"
  annotationProcessor "org.projectlombok:lombok:$lombok_version"

  compileOnly "javax.annotation:jsr250-api:$jsr250_version"

  annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

  implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
  implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
  implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
  implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"
}

// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'